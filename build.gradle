buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
    mavenLocal()
    maven { // Vanilla
        name 'Vanilla'
        url 'https://libraries.minecraft.net/'
    }
    maven { // Forge
        name 'ForgeFS'
        url 'https://maven.minecraftforge.net/'
    }
    maven { // JEI
        name 'DVS1 Maven FS'
        url 'https://dvs1.progwml6.com/files/maven'
    }
    maven { //PenguinLib
        name 'Joshiejack'
        url 'https://maven.joshiejack.uk/'
    }
    maven { //CurseForge
        url "https://cursemaven.com"
    }
    maven {
        // Shedaniel's maven (Architectury API)
        url = "https://maven.architectury.dev"
        content {
            includeGroup "me.shedaniel"
        }
    }
    maven {
        // saps.dev Maven (KubeJS and Rhino)
        // you can also use Lat's Maven @ https://maven.latvian.dev/
        url = "https://maven.saps.dev/minecraft"
        content {
            includeGroup "dev.latvian.mods"
        }
    }
    maven {
        name = "Jared's Maven (CRT)"
        url "https://maven.blamejared.com"
    }
}

ext.build_number=""
if (System.getenv().BUILD_NUMBER) {
    ext.build_number="-" + System.getenv().BUILD_NUMBER
}

version = "${minecraft_version}-${mod_version}${build_number}"
group = 'uk.joshiejack.simplyseasons' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'Simply-Seasons'

java.toolchain.languageVersion = JavaLanguageVersion.of(8) // Mojang ships Java 8 to end users, so your mod should target Java 8.

mixin {
    add sourceSets.main, "simplyseasons.refmap.json"
}

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: '1.16.5'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            arg "-mixin.config=simplyseasons.mixins.json"
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                simplyseasons {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            arg "-mixin.config=simplyseasons.mixins.json"
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                simplyseasons {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', 'simplyseasons', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                simplyseasons {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    annotationProcessor 'org.spongepowered:mixin:0.8:processor'
    minecraft 'net.minecraftforge:forge:1.16.5-36.1.16'
    compile fg.deobf("uk.joshiejack.penguinlib:Penguin-Lib:${minecraft_version}-${penguinlib_version}")
    compileOnly fg.deobf("mezz.jei:jei-${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_version}")
    compileOnly fg.deobf("curse.maven:serene_seasons-291874:${serene_seasons_version}")
    //runtimeOnly fg.deobf("curse.maven:serene_seasons-291874:${serene_seasons_version}")
    compileOnly fg.deobf("curse.maven:better_weather-400714:${better_weather_version}")
    //runtimeOnly fg.deobf("curse.maven:better_weather-400714:${better_weather_version}")
    compileOnly fg.deobf("curse.maven:morpheus-69118:${morpheus_version}")
    runtimeOnly fg.deobf("curse.maven:morpheus-69118:${morpheus_version}")
    compileOnly fg.deobf("me.shedaniel:architectury-forge:${architectury_version}")
    compileOnly fg.deobf("dev.latvian.mods:rhino:${rhino_version}")
    compileOnly fg.deobf("dev.latvian.mods:kubejs-forge:${kubejs_version}")
    compileOnly fg.deobf("net.darkhax.bookshelf:Bookshelf-1.16.5:${bookshelf_version}")
    runtimeOnly fg.deobf("net.darkhax.bookshelf:Bookshelf-1.16.5:${bookshelf_version}")
    compileOnly fg.deobf("net.darkhax.botanybots:BotanyPots-1.16.5:${botanypots_version}")
    runtimeOnly fg.deobf("net.darkhax.botanybots:BotanyPots-1.16.5:${botanypots_version}")

}

jar {
    manifest {
        attributes([
            "Specification-Title": "Simply-Seasons",
            "Specification-Vendor": "joshiejack",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"joshiejack",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "simplyseasons.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//publish.dependsOn('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'Simply-Seasons'
            artifact jar
        }
    }
    repositories {
        if (project.hasProperty('mavenRepository')) {
            maven { url mavenRepository }
        } else {
            mavenLocal()
        }
    }
}
